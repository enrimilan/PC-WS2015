set terminal pngcairo enhanced font 'Verdana'
set samples 1001

# Set grid
set grid
set grid mxtic mytic
set tics scale 1,1 font ", 10"
set key left top
set key font ",10"


# Get input files 
inputDir 		=	"../output/"
inputFiles	 	= 	system(sprintf("ls %s*.dat", inputDir))

# Define some useful functions
select(file, col, val) 	= 	sprintf("<awk '{if ($%d == \"%s\") {print $0}}' %s", col, val, file)
getName(file) 			=	file[strstrt(file, inputDir)+strlen(inputDir) : strstrt(file, '.dat')-1]
getTechnology(log) 		= 	log[0 : strstrt(log, '_')-1]

# Select test sizes and cores to be plotted
sizes 			= 	"1000 10000 50000 100000 500000"
coresSaturn 	= 	"1 8 16 32 48"
coresJupiter	= 	"1 16 96 288 576"


# Draw plots
do for [file in inputFiles] {
	name = getName(file)
	
	# Print speedup as function of processor cores
	set output name . "_cores.png"
	set xlabel "Processor cores"
	set ylabel "Speedup"

	plot for [size in sizes] select(file, 2, size) \
		using 1:6 with lp lw 1.5 pt 7 title "n=".size


	# Print mean execution time as function of test size
	set output name . "_sizes.png"
	set xlabel "Test size"
	set ylabel "Execution time (Âµs)"

	coreList = (getTechnology(name) eq "MPI") ? coresJupiter : coresSaturn
	plot for [cores in coreList] select(file, 1, cores) \
			using 2:5 with lp lw 1.5 pt 7 title "p=".cores
}